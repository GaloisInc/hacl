/* Example instrumented to collect measurements for iFM paper. */
zucbc <- llvm_load_module "zuc.bc";
zuc14bc <- llvm_load_module "zuc14.bc";
zuccry <- cryptol_load "zuc2.cry";

let picosat = external_cnf_solver "picosat" ["%f"];
let proof_wrap t = do { print_goal_size; t; };

k <- fresh_symbolic "k" {| [16][8] |};
iv1 <- fresh_symbolic "iv1" {| [16][8] |};
iv2 <- fresh_symbolic "iv2" {| [16][8] |};

let allocs = [ ("k", 16), ("iv", 16) ];
let results =
  [ ("LFSR_S0", 1)
  , ("LFSR_S1", 1)
  , ("LFSR_S2", 1)
  , ("LFSR_S3", 1)
  , ("LFSR_S4", 1)
  , ("LFSR_S5", 1)
  , ("LFSR_S6", 1)
  , ("LFSR_S7", 1)
  , ("LFSR_S8", 1)
  , ("LFSR_S9", 1)
  , ("LFSR_S10", 1)
  , ("LFSR_S11", 1)
  , ("LFSR_S12", 1)
  , ("LFSR_S13", 1)
  , ("LFSR_S14", 1)
  , ("LFSR_S15", 1)
  , ("F_R1", 1)
  , ("F_R2", 1)
  ];

print "Proving absence of ZUC 1.4 IV weakness in ZUC 1.5.";

init1 <- llvm_symexec zucbc "InitializationOne"
  allocs
  [ ("*k", {{ k }}, 16)
  , ("*iv", {{ iv1 }}, 16)
  , ("F_R1", {{ 0 }}, 1)
  , ("F_R2", {{ 0 }}, 1)
  ]
  results
  false;
init2 <- llvm_symexec zucbc "InitializationOne"
  allocs
  [ ("*k", {{ k }}, 16)
  , ("*iv", {{ iv2 }}, 16)
  , ("F_R1", {{ 0 }}, 1)
  , ("F_R2", {{ 0 }}, 1)
  ]
  results
  false;
thm2 <- abstract_symbolic {{ iv1 == iv2 || init1 != init2 }};

print "Shared size";
print (term_size thm2);
print "Unhared size";
print (term_tree_size thm2);

print "";
print "ABC";
time (prove_print abc thm2);

// Very slow (>20m)
/*
print "";
print "CVC4";
time (prove_print cvc4 thm2);
*/

print "";
print "Yices";
time (prove_print yices thm2);

print "";
print "Z3";
time (prove_print z3 thm2);

print "";
print "Picosat";
time (prove_print picosat thm2);
